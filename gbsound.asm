; 2's complement negation
neg: MACRO
    cpl
    inc a
ENDM


SECTION "gbsound", ROM0

;
; Lookup the current value of the vibrato for a given index
; vibrato indices represent an angle in a sine period
; such that the angle = index/64 * 2pi
;  0 <= index < 16: quadrant I
; 16 <= index < 32: quadrant II
; 32 <= index < 48: quadrant III
; 48 <= index < 64: quadrant IV
;
; params:
;  b - index of sine waveform (0-63)
;  c - extent (0-15)
;
; returns:
;  a - vibrato value (signed)
;
lookupVibrato::
    push hl
    push de
    ld a, b
    and a, $1F
    jp z, .exit
    ld h, $0           ; hl <- extent * 16
    ld l, c
    sla l
    sla l
    sla l
    sla l
    
    ld de, VibratoTable
    add hl, de              ; hl now points to a vibrato table for the given extent
    cp a, $10
    jp c, .quad1
    ; else quad2
    ; a = a - 16
    sub a, $10
    jp .lookupValue
.quad1:
    ; a = 16 - a  =>  a = -a + 16
    neg
    add a, $10
.lookupValue:
    ld d, $0
    ld e, a                 ; de <- a
    add hl, de
    ld e, [hl]              ; save this for later
    ld a, b                 ; check if the index is in quadrant 3 or 4
    cp a, $20               ; is a >= 32 (quadrants 3 and 4)
    ld a, e                 ; restore our saved lookup
    jp c, .exit             ; jump if index was < 32, no need to negate (quads 1 and 2)
    neg                     ; index is >= 32, negate the vibrato value
.exit:
    ; a contains the result so now we can return
    pop de
    pop hl
    ret



; Note frequency table
; Octave range for CH1/CH2 is 2-8, CH3 is one lower (unless the waveform has 2 periods)
; Note indices start at C-2 and end at B-8

NoteTable:
;        C     C#    D     D#    E     F     F#    G     G#    A     A#    B
    dw $02C, $09D, $107, $16B, $1CA, $223, $277, $2C7, $312, $358, $39B, $3DA ; Octave 2
    dw $416, $44E, $483, $4B5, $4E5, $511, $53C, $563, $589, $5AC, $5CE, $5ED ; Octave 3
    dw $60B, $627, $642, $65B, $672, $689, $69E, $6B2, $6C4, $6D6, $6E7, $6F7 ; Octave 4
    dw $706, $714, $721, $72D, $739, $744, $74F, $759, $762, $76B, $773, $77B ; Octave 5
    dw $783, $78A, $790, $797, $79D, $7A2, $7A7, $7AC, $7B1, $7B6, $7BA, $7BE ; Octave 6
    dw $7C1, $7C5, $7C8, $7CB, $7CE, $7D1, $7D4, $7D6, $7D9, $7DB, $7DD, $7DF ; Octave 7
    dw $7E1, $7E2, $7E4, $7E6, $7E7, $7E9, $7EA, $7EB, $7EC, $7ED, $7EE, $7EF ; Octave 8

; Vibrato tables
; A vibrato table contains quadrant II of a sine wave for 16 possible extents
; Each table contains 16 samples of the quadrant

VibratoTable:
    ; this table was generated by genvibrato.py
    ; $ genvibrato.py asm
    db $01, $01, $01, $01, $01, $01, $01, $01, $01, $01, $01, $00, $00, $00, $00, $00 ; extent =   1
    db $02, $02, $02, $02, $02, $02, $02, $02, $01, $01, $01, $01, $01, $01, $00, $00 ; extent =   2
    db $03, $03, $03, $03, $03, $03, $02, $02, $02, $02, $02, $01, $01, $01, $01, $00 ; extent =   3
    db $04, $04, $04, $04, $04, $04, $03, $03, $03, $03, $02, $02, $02, $01, $01, $00 ; extent =   4
    db $05, $05, $05, $05, $05, $04, $04, $04, $04, $03, $03, $02, $02, $01, $01, $00 ; extent =   5
    db $07, $07, $07, $07, $06, $06, $06, $05, $05, $04, $04, $03, $03, $02, $01, $01 ; extent =   7
    db $0A, $0A, $0A, $0A, $09, $09, $08, $08, $07, $06, $06, $05, $04, $03, $02, $01 ; extent =  10
    db $0C, $0C, $0C, $0B, $0B, $0B, $0A, $09, $08, $08, $07, $06, $05, $03, $02, $01 ; extent =  12
    db $0E, $0E, $0E, $0D, $0D, $0C, $0C, $0B, $0A, $09, $08, $07, $05, $04, $03, $01 ; extent =  14
    db $11, $11, $11, $10, $10, $0F, $0E, $0D, $0C, $0B, $09, $08, $07, $05, $03, $02 ; extent =  17
    db $16, $16, $16, $15, $14, $13, $12, $11, $10, $0E, $0C, $0A, $08, $06, $04, $02 ; extent =  22
    db $1E, $1E, $1D, $1D, $1C, $1A, $19, $17, $15, $13, $11, $0E, $0B, $09, $06, $03 ; extent =  30
    db $2C, $2C, $2B, $2A, $29, $27, $25, $22, $1F, $1C, $18, $15, $11, $0D, $09, $04 ; extent =  44
    db $40, $40, $3F, $3D, $3B, $38, $35, $31, $2D, $29, $24, $1E, $18, $13, $0C, $06 ; extent =  64
    db $60, $60, $5E, $5C, $59, $55, $50, $4A, $44, $3D, $35, $2D, $25, $1C, $13, $09 ; extent =  96
    db $7F, $7E, $7D, $7A, $75, $70, $6A, $62, $5A, $51, $47, $3C, $31, $25, $19, $0C ; extent = 127